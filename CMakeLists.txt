CMAKE_MINIMUM_REQUIRED(VERSION 2.8.0)
PROJECT(duckos C CXX ASM)

set(TOOLCHAIN_DIR ${CMAKE_SOURCE_DIR}/toolchain/tools/bin/)
set(TOOLCHAIN_PREFIX i686-pc-duckos-)

set(CMAKE_C_COMPILER ${TOOLCHAIN_DIR}${TOOLCHAIN_PREFIX}gcc)
set(CMAKE_CXX_COMPILER ${TOOLCHAIN_DIR}${TOOLCHAIN_PREFIX}g++)
set(CMAKE_LINKER ${TOOLCHAIN_DIR}${TOOLCHAIN_PREFIX}ld)
set(CMAKE_ASM_COMPILER ${TOOLCHAIN_DIR}${TOOLCHAIN_PREFIX}gcc)
set(CMAKE_STRIP ${TOOLCHAIN_DIR}${TOOLCHAIN_PREFIX}strip)
set(CMAKE_AR ${TOOLCHAIN_DIR}${TOOLCHAIN_PREFIX}ar)
set(CMAKE_RANLIB ${TOOLCHAIN_DIR}${TOOLCHAIN_PREFIX}ranlib)

set(CMAKE_STAGING_PREFIX ${CMAKE_BINARY_DIR}/root)
set(CMAKE_INSTALL_PREFIX ${CMAKE_BINARY_DIR}/root)
set(CMAKE_INSTALL_DATAROOTDIR ${CMAKE_BINARY_DIR}/root/res)

set(CMAKE_SHARED_LIBRARY_SUFFIX ".so")
set(CMAKE_SHARED_LIBRARY_CREATE_CXX_FLAGS "-shared")
set(CMAKE_SHARED_LIBRARY_CREATE_C_FLAGS "-shared")
set(BUILD_SHARED_LIBS ON)
SET(CMAKE_SHARED_LIBRARY_LINK_C_FLAGS "")
SET(CMAKE_SHARED_LIBRARY_LINK_CXX_FLAGS "")

function(MAKE_LIBRARY LIBNAME)
    # Install dynamic library
    ADD_LIBRARY(${LIBNAME} ${SOURCES})
    INSTALL(TARGETS ${LIBNAME} LIBRARY DESTINATION usr/lib)
    SET_TARGET_PROPERTIES(${LIBNAME} PROPERTIES PREFIX "")

    # Install dynamic library
    ADD_LIBRARY(${LIBNAME}_static STATIC ${SOURCES})
    INSTALL(TARGETS ${LIBNAME}_static LIBRARY DESTINATION usr/lib)
    SET_TARGET_PROPERTIES(${LIBNAME}_static PROPERTIES PREFIX "")

    # Install headers
    FILE(GLOB_RECURSE LIBHEADERS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} "*.h")
    foreach(HEADER ${LIBHEADERS})
        GET_FILENAME_COMPONENT(SUBDIRECTORY ${HEADER} DIRECTORY)
        install(FILES ${HEADER} DESTINATION usr/include/${LIBNAME}/${SUBDIRECTORY})
    endforeach()
endfunction()

function(MAKE_PROGRAM PROGNAME)
    ADD_EXECUTABLE(${PROGNAME} ${SOURCES})
    INSTALL(TARGETS ${PROGNAME} RUNTIME DESTINATION bin)
endfunction()

INCLUDE_DIRECTORIES(libraries/)
INCLUDE_DIRECTORIES(libraries/libc)
INCLUDE_DIRECTORIES(.)
ADD_SUBDIRECTORY(kernel/)
ADD_SUBDIRECTORY(libraries/)
ADD_SUBDIRECTORY(services/)
ADD_SUBDIRECTORY(programs/)
ADD_SUBDIRECTORY(ports/)

ADD_CUSTOM_TARGET(image
        COMMAND ${CMAKE_COMMAND} -E env "SOURCE_DIR=${CMAKE_SOURCE_DIR}" ${CMAKE_SOURCE_DIR}/scripts/image.sh
        BYPRODUCTS ${CMAKE_BINARY_DIR}/duckOS.img
        USES_TERMINAL
)

ADD_CUSTOM_TARGET(qemu-kernel
        COMMAND ${CMAKE_SOURCE_DIR}/scripts/qemu-kernel.sh
        USES_TERMINAL
)

ADD_CUSTOM_TARGET(qemu-image
        COMMAND ${CMAKE_SOURCE_DIR}/scripts/qemu-image.sh
        USES_TERMINAL
)
